plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
def credentialFile = rootProject.file('credential.properties')
def credential
android {
    if (credentialFile.exists()) {
        credential = new Properties()
        credential.load(new FileInputStream(credentialFile))
        signingConfigs {
            config {
                keyAlias credential['keyAlias']
                keyPassword credential['keyPassword']
                storeFile file(credential['storeFile'])
                storePassword credential['storePassword']
            }
        }
    }
    namespace 'app.familygem'
    compileSdk 34
    defaultConfig {
        applicationId 'app.familygem'
        minSdk 19
        targetSdk 34
        versionCode 26
        versionName '1.0.1.1'
        vectorDrawables.useSupportLibrary = true
        resourceConfigurations += ['ar', 'ckb', 'cs', 'da', 'de', 'en', 'eo', 'es', 'fa', 'fr', 'hr', 'hu', 'in', 'it', 'iw',
                                   'kn', 'lt', 'mr', 'nb', 'nl', 'pl', 'pt', 'ru', 'sk', 'sl', 'sr', 'ti', 'tr', 'uk', 'zh']
        def passKey = ''
        if (credential) {
            passKey = credential['passKey']
        }
        buildConfigField 'String', 'PASS_KEY', '"' + passKey + '"'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled true
    }
    buildTypes {
        debug {
            pseudoLocalesEnabled true
            if (credential)
                signingConfig signingConfigs.config
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (credential)
                signingConfig signingConfigs.config
        }
    }
    buildFeatures {
        buildConfig true
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    // commons-io versions >= 2.9.0 throw NoClassDefFoundError on API <= 25
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'commons-net:commons-net:3.10.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.7.0'
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'
    implementation 'org.familysearch.gedcom:gedcom:1.14.0'
    implementation 'androidx.work:work-runtime:2.9.0'
    implementation 'androidx.appcompat:appcompat:1.7.0-alpha03'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.6'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.android.billingclient:billing-ktx:6.1.0'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation files('libs/gedcomgraph-3.5.jar')
    implementation 'joda-time:joda-time:2.12.7'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
    implementation 'com.otaliastudios:zoomlayout:1.9.0'
    implementation 'com.github.bumptech.glide:glide:4.16.0'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'com.github.AndroidDeveloperLB:FastScrollerAndRecyclerViewFixes:9'
    implementation files('libs/geonames-1.1.15.jar')
    // Don't update Jdom to version 2.x for compatibility with GeoNames 1.1.15
    implementation 'org.jdom:jdom:1.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.1'
}
