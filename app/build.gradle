apply plugin: 'com.android.application'

def fileChiavi = rootProject.file("chiavi.properties")
def chiave

android {
	if( fileChiavi.exists() ) {
		chiave = new Properties()
		chiave.load( new FileInputStream(fileChiavi) )
		signingConfigs {
			configa {
				keyAlias chiave['keyAlias']
				keyPassword chiave['keyPassword']
				storeFile file(chiave['storeFile'])
				storePassword chiave['storePassword']
			}
		}
	}
	compileSdkVersion 29
	defaultConfig {
		applicationId "app.familygem"
		minSdkVersion 19
		targetSdkVersion 29
		versionCode 17
		versionName '0.7.14'
		vectorDrawables.useSupportLibrary = true
		resConfigs "en", "de", "es", "fr", "hr", "it", "nb-rNO", "nl-rBE", "pl", "uk"
		def backupKey = ""
		def utenteAruba = ""
		def passwordAruba = ""
		def utenteGeoNames = "demo"
		if (chiave) {
			backupKey = chiave['backupKey']
			utenteAruba = chiave['utenteAruba']
			passwordAruba = chiave['passwordAruba']
			utenteGeoNames = chiave['utenteGeoNames']
		}
		resValue "string", "backupKey", backupKey
		buildConfigField "String", "utenteAruba", "\""+utenteAruba+"\""
		buildConfigField "String", "passwordAruba", "\""+passwordAruba+"\""
		buildConfigField "String", "utenteGeoNames", "\""+utenteGeoNames+"\""
	}
	buildTypes {
		debug {
			if (chiave)
				signingConfig signingConfigs.configa
		}
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),'proguard-rules.pro'
			if (chiave)
				signingConfig signingConfigs.configa
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	packagingOptions {
		excludes += ['**/*.properties','META-INF/*.md','META-INF/*.xml']
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'org.familysearch.gedcom:gedcom:1.11.0'
	implementation 'androidx.appcompat:appcompat:1.2.0'
	implementation 'androidx.legacy:legacy-support-v4:1.0.0'
	implementation 'androidx.recyclerview:recyclerview:1.1.0'
	implementation 'androidx.cardview:cardview:1.0.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
	implementation 'com.google.android.material:material:1.2.1'
	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'commons-io:commons-io:2.6'
	implementation 'commons-net:commons-net:3.6'
	implementation 'org.jsoup:jsoup:1.12.1'
	implementation 'com.otaliastudios:zoomlayout:1.3.0'
	implementation 'joda-time:joda-time:2.10.5'
	implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
	implementation 'com.squareup.picasso:picasso:2.71828'
	implementation 'org.jdom:jdom:1.1.3'
	implementation 'com.android.installreferrer:installreferrer:2.1'
	//implementation 'jaxen:jaxen:1.2.0'
}
