plugins {
    id 'com.android.application'
    id 'kotlin-android'
}
def credentialFile = rootProject.file('credential.properties')
def credential
android {
    if (credentialFile.exists()) {
        credential = new Properties()
        credential.load(new FileInputStream(credentialFile))
        signingConfigs {
            config {
                keyAlias credential['keyAlias']
                keyPassword credential['keyPassword']
                storeFile file(credential['storeFile'])
                storePassword credential['storePassword']
            }
        }
    }
    namespace = 'app.familygem'
    compileSdk = 36
    defaultConfig {
        applicationId = 'app.familygem'
        minSdk = 21
        targetSdk = 36
        versionCode = 28
        versionName = '1.2.0.3'
        resourceConfigurations += ['ar', 'bg', 'ckb', 'cs', 'da', 'de', 'en', 'eo', 'es', 'fa', 'fr', 'hr', 'hu', 'in', 'it', 'iw', 'kn',
                                   'lt', 'mr', 'nb', 'nl', 'pl', 'pt', 'ru', 'sk', 'sl', 'sr', 'sv', 'ta', 'ti', 'tr', 'uk', 'vi', 'zh']
        def passKey = ''
        if (credential) {
            passKey = credential['passKey']
        }
        buildConfigField 'String', 'PASS_KEY', '"' + passKey + '"'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        multiDexEnabled = true
    }
    buildTypes {
        debug {
            pseudoLocalesEnabled = true
            if (credential)
                signingConfig = signingConfigs.config
        }
        release {
            shrinkResources = true
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (credential)
                signingConfig = signingConfigs.config
        }
    }
    buildFeatures {
        buildConfig = true
        viewBinding = true
        dataBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    // commons-io versions >= 2.9.0 throws NoClassDefFoundError on API <= 25
    implementation 'commons-io:commons-io:2.8.0'
    // commons-net versions >= 3.9.0 throws NoClassDefFoundError on API <= 25
    implementation 'commons-net:commons-net:3.8.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'org.familysearch.gedcom:gedcom:1.14.0'
    implementation 'androidx.lifecycle:lifecycle-process:2.9.4' // Version >= 2.10 requires API >= 23
    implementation 'androidx.security:security-crypto:1.1.0'
    implementation 'androidx.work:work-runtime:2.10.5' // Version >= 2.11 requires API >= 23
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.documentfile:documentfile:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.6'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0'
    implementation 'com.android.installreferrer:installreferrer:2.2'
    implementation 'com.android.billingclient:billing-ktx:8.0.0' // Version >= 8.1 requires API >= 23
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation files('libs/gedcomgraph-3.10.jar')
    implementation 'joda-time:joda-time:2.14.0'
    implementation 'com.vanniktech:android-image-cropper:4.6.0'
    implementation 'io.github.panpf.zoomimage:zoomimage-view-glide:1.4.0'
    implementation 'com.github.bumptech.glide:glide:5.0.5'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'me.zhanghai.android.fastscroll:library:1.3.0'
    implementation files('libs/geonames-1.1.15.jar')
    // Don't update Jdom to version 2.x for compatibility with GeoNames 1.1.15
    implementation 'org.jdom:jdom:1.1.3'
    // To avoid ClassNotFoundException org.apache.xerces.parsers.SAXParser when building with minifyEnabled
    implementation 'xerces:xercesImpl:2.12.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2'
}
