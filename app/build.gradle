apply plugin: 'com.android.application'

def fileChiavi = rootProject.file("chiavi.properties")
def chiave

android {
	if( fileChiavi.exists() ) {
		chiave = new Properties()
		chiave.load(new FileInputStream(fileChiavi))
		signingConfigs {
			configa {
				keyAlias chiave['keyAlias']
				keyPassword chiave['keyPassword']
				storeFile file(chiave['storeFile'])
				storePassword chiave['storePassword']
			}
		}
	}
	compileSdkVersion 32
	defaultConfig {
		applicationId "app.familygem"
		minSdkVersion 19
		targetSdkVersion 32
		versionCode 20
		versionName '0.9'
		vectorDrawables.useSupportLibrary = true
		resConfigs "en", "cs", "de", "eo", "es", "fa", "fr", "hr", "hu", "in", "it", "iw", "kn", "mr", "nb", "nl", "pl", "pt", "ru", "sk", "sr", "uk"
		def utenteAruba = ""
		def passwordAruba = ""
		def utenteGeoNames = "demo"
		if (chiave) {
			utenteAruba = chiave['utenteAruba']
			passwordAruba = chiave['passwordAruba']
			utenteGeoNames = chiave['utenteGeoNames']
		}
		buildConfigField "String", "utenteAruba", "\""+utenteAruba+"\""
		buildConfigField "String", "passwordAruba", "\""+passwordAruba+"\""
		buildConfigField "String", "utenteGeoNames", "\""+utenteGeoNames+"\""
		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		multiDexEnabled true
	}
	buildTypes {
		debug {
			pseudoLocalesEnabled true
			if (chiave)
				signingConfig signingConfigs.configa
		}
		release {
			shrinkResources true
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
			if (chiave)
				signingConfig signingConfigs.configa
		}
	}
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	packagingOptions {
		excludes += ['**/*.properties', 'META-INF/*.md', 'META-INF/*.xml']
	}
}

dependencies {
	implementation fileTree(include: ['*.jar'], dir: 'libs')
	implementation 'org.familysearch.gedcom:gedcom:1.11.0'
	implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
	implementation 'androidx.lifecycle:lifecycle-process:2.4.1'
	implementation 'androidx.work:work-runtime:2.7.1'
	implementation 'com.google.android.material:material:1.6.0'
	implementation 'com.google.code.gson:gson:2.8.7'
	implementation 'commons-io:commons-io:2.11.0'
	implementation 'commons-net:commons-net:3.6'
	implementation 'org.jsoup:jsoup:1.14.1'
	implementation 'joda-time:joda-time:2.10.10'
	implementation 'com.otaliastudios:zoomlayout:1.3.0'
	implementation 'com.theartofdev.edmodo:android-image-cropper:2.8.0'
	implementation 'com.squareup.picasso:picasso:2.71828'
	implementation 'org.jdom:jdom:1.1.3' // Don't update to version 2.x for compatibility with GeoNames
	implementation 'com.android.installreferrer:installreferrer:2.2'
	implementation 'com.github.AndroidDeveloperLB:FastScrollerAndRecyclerViewFixes:5'
	//implementation 'jaxen:jaxen:1.2.0'
	androidTestImplementation 'androidx.test:runner:1.4.0'
}
